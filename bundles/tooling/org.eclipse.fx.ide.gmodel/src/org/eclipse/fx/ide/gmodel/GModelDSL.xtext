grammar org.eclipse.fx.ide.gmodel.GModelDSL with org.eclipse.xtext.common.Terminals

generate gModelDSL "http://www.eclipse.org/fx/ide/gmodel/GModelDSL"

GModel:
	'package' name=QualifiedName '{'
		typeList += GDomainElement+
	'}'
;

GDomainElement:
	'type' name=ID ('extends' superTypeList+=[GDomainElement|ID] ( ', ' superTypeList+=[GDomainElement|ID])* )? '{'
		(map=GDomainMap | propertyList += GDomainProperty*)
	'}'
;

GDomainMap:
	'map' (builtIn=('Int'|'Double'|'String'|'Boolean') | ref=[GDomainElement|ID] )
;

GDomainProperty:
	((builtIn=('Int'|'Double'|'String'|'Boolean') list?='[]'? name=ID ('=' defaultValue = GDefault)? ) | ((ref=[GDomainElement|ID]) list?='[]'? name=ID)) ';'
;

GDefault:
	stringVal=STRING | intVal = INT
;

ValidID:
	ID;

QualifiedName:
  ValidID (=>'.' ValidID)*;